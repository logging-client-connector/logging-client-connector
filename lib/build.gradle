plugins {
    id 'java'
    id 'groovy'
    id 'jacoco'
    id 'java-library'
    id 'maven-publish'
    id 'org.springframework.boot' version '3.3.2' apply(false)
    id 'io.spring.dependency-management' version '1.1.6'
}

group = 'org.loggingclientconnector'
version = '1.0.0'

java {
    withJavadocJar()
    withSourcesJar()
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-webflux:3.3.2'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-xml:2.17.2'

    testImplementation 'org.springframework.boot:spring-boot-starter-test:3.3.2'
    testImplementation 'io.projectreactor:reactor-test:3.6.8'

    testImplementation 'org.apache.groovy:groovy:4.0.16'

    testImplementation 'org.spockframework:spock-core:2.4-M1-groovy-4.0'
    testImplementation 'org.spockframework:spock-spring:2.4-M1-groovy-4.0'

    testImplementation 'org.mock-server:mockserver-netty:5.15.0'
    testImplementation 'org.mock-server:mockserver-client-java:5.15.0'
}

jacocoTestReport {
    dependsOn test
    reports {
        xml.required = true
        html.required = true
    }
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy('jacocoTestReport')
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java

            pom {
                name = 'Logging Client Connector'
                description = 'A library for logging Spring Webclient requests and responses.'
                url = 'https://github.com/logging-client-connector/logging-client-connector'
                licenses {
                    license {
                        name = 'The MIT License'
                        url = 'https://opensource.org/licenses/MIT'
                    }
                }
                developers {
                    developer {
                        id = 'rafalkawczynski'
                        name = 'Rafał Kawczyński'
                        email = 'rafal.kawczynski@outlook.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/logging-client-connector/logging-client-connector.git'
                    developerConnection = 'scm:git:ssh://github.com:logging-client-connector/logging-client-connector.git'
                    url = 'https://github.com/logging-client-connector/logging-client-connector'
                }
            }
        }
    }

    repositories {
        maven {
            name = 'jitpack'
            url = uri('https://jitpack.io')
        }
    }
}